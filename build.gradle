buildscript {
    repositories {
        maven{ url 'http://maven.aliyun.com/nexus/content/groups/public/'}
    }
}

plugins {
    id "edu.sc.seis.launch4j" version "2.3.0"
}

allprojects {
    repositories {
        maven{ url 'http://maven.aliyun.com/nexus/content/groups/public/'}
    }
}

apply plugin: "java"

dependencies {
    // to compile Mac specific classes on non-Mac platform
    compileOnly "com.yuvimasory:orange-extensions:1.3.0"
}


version = "0.5.0"

sourceSets {
    main {
        java { srcDirs = ["src"] }
    }
    app {
        resources { srcDirs = ["resources"] }
    }
}

compileJava {
    options.compilerArgs << "-XDignore.symbol.file=true" << "-Xlint:unchecked" << "-target" << "1.6" << "-source" << "1.6"
    options.fork = true
    options.forkOptions.executable = "javac"
}

def appMainClass = "org.jchmlib.app.ChmWebApp"

javadoc {
    exclude "org/jchmlib/app/**"
    options.locale = "en_US"
    options.encoding = "UTF-8"
}

task appJar(type: Jar) {
    baseName = "ChmWeb"

    manifest {
        attributes(
                "Implementation-Title": baseName,
                "Implementation-Version": version,
                "Main-Class": appMainClass)
    }

    from("LICENSE")
    from(sourceSets.main.output)
    from(sourceSets.app.resources)
}

task libJar(type: Jar) {
    baseName = "jchmlib"

    manifest {
        attributes(
                "Implementation-Title": "jchmlib",
                "Implementation-Version": version)
    }

    from("LICENSE")
    from(sourceSets.main.output) {
        exclude "org/jchmlib/app/**"
    }
}

jar {
    actions = [] // reset actions
    dependsOn libJar, appJar
}

def appName = appJar.baseName
def appJarFile = appJar.outputs.files[0].path
def deployDir = "${buildDir}/deploy"
def runtime = System.getProperty("java.home")

if (System.properties['os.name'].contains("Linux")) {
    for (nativeType in ["image", "rpm", "deb", "installer"]) {
        task "package_${nativeType}"(dependsOn: appJar) {
            group = "distribution"

            def customRuntime = "$rootDir/package/linux/jdk"
            File testFile = new File(customRuntime)
            if (testFile.exists()) {
                runtime = customRuntime
            }

            doLast {
                file(deployDir).mkdirs()
                exec {
                    executable System.getProperty("java.home")+"/../bin/javapackager"
                    args "-deploy", "-title", appName, "-name", appName, "-appclass", appMainClass,
                         "-native", nativeType, "-outdir", deployDir, "-outfile", appName,
                         "-srcfiles", appJarFile, "-Bruntime=$runtime", "-verbose",
                         "-BappVersion=$version", "-Bcopyright=Copyright Â© 2017 chimenchen. All rights reserved.",
                         "-BlicenseType=Apache License 2.0", "-srcfiles", "LICENSE", "-BlicenseFile=LICENSE"
                }
            }
        }
    }
}

// NOTE: need to change Info.plist if version/appName/jdk is changed.
// FIXME: generate Info.plist?
if (System.properties['os.name'].contains("Mac")) {
    for (nativeType in ["image", "dmg", "installer"]) {
        task "package_${nativeType}"(dependsOn: appJar) {
            group = "distribution"

            def customRuntime = "$rootDir/package/macosx/jdk"
            File testFile = new File(customRuntime)
            if (testFile.exists()) {
                runtime = customRuntime
            }

            doLast {
                file(deployDir).mkdirs()
                exec {
                    executable System.getProperty("java.home")+"/../bin/javapackager"
                    args "-deploy", "-title", appName, "-name", appName, "-appclass", appMainClass,
                         "-native", nativeType, "-outdir", deployDir, "-outfile", appName,
                         "-srcfiles", appJarFile, "-BappVersion=$version", "-Bruntime=$runtime", "-verbose"
                }
            }
        }
    }
}

if (System.properties['os.name'].contains("Windows")) {
    for (nativeType in ["image", "exe", "msi", "installer"]) {
        task "package_${nativeType}"(dependsOn: appJar) {
            group = "distribution"

            def customRuntime = "$rootDir/package/windows/jdk"
            File testFile = new File(customRuntime)
            if (testFile.exists()) {
                runtime = customRuntime
            }

            doLast {
                file(deployDir).mkdirs()
                exec {
                    executable System.getProperty("java.home")+"/../bin/javapackager"
                    args "-deploy", "-title", appName, "-name", appName, "-appclass", appMainClass,
                         "-native", nativeType, "-outdir", deployDir, "-outfile", appName,
                         "-srcfiles", appJarFile, "-BappVersion=$version", "-Bruntime=$runtime", "-verbose"
                }
            }
        }
    }
}

launch4j {
  mainClassName = appMainClass
  icon = "${project.rootDir}/package/windows/ChmWeb.ico"
  jar = appJar.outputs.files[0].path
  outfile = appJar.baseName + ".exe"
  productName = appJar.baseName
  version = project.version
  fileDescription = "A web server for viewing CHM files in web browser"
}


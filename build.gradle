buildscript {
    repositories {
        maven{ url 'http://maven.aliyun.com/nexus/content/groups/public/'}
    }
}

plugins {
    id "edu.sc.seis.launch4j" version "2.3.0"
}

allprojects {
    repositories {
        maven{ url 'http://maven.aliyun.com/nexus/content/groups/public/'}
    }
}

apply plugin: "java"

dependencies {
    // to compile Mac specific classes on non-Mac platform
    compileOnly "com.yuvimasory:orange-extensions:1.3.0"
}


version = "0.2"

sourceSets {
    main {
        java { srcDirs = ["src"] }
    }
    app {
        resources { srcDirs = ["resources"] }
    }
}

compileJava {
    options.compilerArgs << "-XDignore.symbol.file=true" << "-Xlint:unchecked"
    options.fork = true
    options.forkOptions.executable = "javac"
}

def mainClassName = "org.jchmlib.app.ChmWebApp"

task appJar(type: Jar) {
    baseName = "ChmWeb"

    manifest {
        attributes(
                "Implementation-Title": baseName,
                "Implementation-Version": version,
                "Main-Class": mainClassName)
    }

    from("LICENSE")
    from(sourceSets.main.output)
    from(sourceSets.app.resources)
}

task libJar(type: Jar) {
    baseName = "jchmlib"

    manifest {
        attributes(
                "Implementation-Title": "jchmlib",
                "Implementation-Version": version)
    }

    from("LICENSE")
    from(sourceSets.main.output) {
        exclude "org/jchmlib/app/**"
    }
}

jar {
    actions = [] // reset actions
    dependsOn libJar, appJar
}

// $(/usr/libexec/java_home)/bin/javapackager -deploy -native dmg -srcfiles build/libs/ChmWeb-0.2.jar -appclass org.jchmlib.app.ChmWebApp -name ChmWeb -outdir build/deploy -outfile ChmWeb -v -BappVersion=0.2

task deb(dependsOn: appJar) {
    def appName = appJar.baseName
    def jarFile = appJar.outputs.files[0].path
    def deployDir = "${buildDir}/deploy"

    file(deployDir).mkdirs()

    doLast {
        exec {
            executable System.getProperty("java.home")+"/../bin/javapackager"
            args "-deploy",
                 "-title", appName,
                 "-name", appName,
                 "-appclass", mainClassName,
                 "-native", "deb",
                 "-outdir", deployDir,
                 "-outfile", appName,
                 "-srcfiles", jarFile,
                 "-BappVersion=$version",
                 "-Bcopyright=Copyright Â© 2017 Chimen Chen. All rights reserved.",
                 "-BlicenseType=Apache License 2.0",
                 "-srcfiles", "LICENSE",
                 "-BlicenseFile=LICENSE",
                 "-verbose"
    }}
}

task dmg(dependsOn: appJar) {
    // NOTE: need to change Info.plist if version or appName is changed.
    def appName = appJar.baseName
    def jarFile = appJar.outputs.files[0].path
    def deployDir = "${buildDir}/deploy"

    file(deployDir).mkdirs()

    doLast { exec {
        executable System.getProperty("java.home")+"/../bin/javapackager"
        args "-deploy",
             "-title", appName,
             "-name", appName,
             "-appclass", mainClassName,
             "-native", "dmg",
             "-outdir", deployDir,
             "-outfile", appName,
             "-srcfiles", jarFile,
             "-BappVersion=$version",
             "-verbose"
    }}
}

launch4j {
  mainClassName = mainClassName
  icon = "${project.rootDir}/package/windows/ChmWeb.ico"
  jar = appJar.outputs.files[0].path
  outfile = appJar.baseName + ".exe"
  productName = appJar.baseName
  fileDescription = "A web server for viewing CHM files in web browser"
}

